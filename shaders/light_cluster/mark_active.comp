#version 460

#include "../include/common.glsl"
#include "../include/types.glsl"

#include "cluster_common.glsl"

layout(push_constant, std430) uniform PushConstants {
    uvec3 cluster_count;
	uint tile_size_px;

	uvec2 screen_size;
	float z_near;
    float z_far;

    float z_scale;
    float z_bias;
    uint depth_buffer;
    uint depth_buffer_sample_count;
    
    uint depth_slice_mask_buffer;
    uint depth_bounds_buffer;
};

layout (local_size_x = 8, local_size_y = 8) in;

void proccess_depth(uvec2 tile_id, float depth, inout uint bitmask) {
    float linear_z = z_near / depth;
    uint slice = linear_z_to_depth_slice(z_scale, z_bias, linear_z);
    bitmask |= 1 << slice;
    if (slice < cluster_count.z) {
        uint cluster_index = cluster_volume_index_from_id(cluster_count, uvec3(tile_id, slice));
        atomicMax(GetBuffer(ClusterDepthBoundsBuffer, depth_bounds_buffer).clusters[cluster_index].min_depth, floatBitsToUint(1.0 - depth));
        atomicMax(GetBuffer(ClusterDepthBoundsBuffer, depth_bounds_buffer).clusters[cluster_index].max_depth, floatBitsToUint(depth));
    }
}

void main() {
    uvec2 pixel_id = gl_GlobalInvocationID.xy;
    if (pixel_id.x >= screen_size.x || pixel_id.y >= screen_size.y) return;
    
    uvec2 tile_id = pixel_id / uvec2(tile_size_px);
    uint bitmask = 0;
    
    if (depth_buffer_sample_count == 1) {
        proccess_depth(tile_id, texelFetch(GetSampledTexture2D(depth_buffer), ivec2(pixel_id), 0).x, bitmask);
    } else {
        for (int i = 0; i < depth_buffer_sample_count; i++) {
            proccess_depth(tile_id, texelFetch(GetSampledTexture2DMS(depth_buffer), ivec2(pixel_id), i).x, bitmask);
        }
    }

    if (bitmask > 0) {
        uint tile_index = tile_id_to_tile_index(cluster_count.xy, tile_id);
        atomicOr(GetBuffer(TileDepthSliceMask, depth_slice_mask_buffer).masks[tile_index], bitmask);
    }
}